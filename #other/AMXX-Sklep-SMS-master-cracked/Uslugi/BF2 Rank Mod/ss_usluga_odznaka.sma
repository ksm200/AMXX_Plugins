/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <colorchat>
#include <smsshop>

new g_iUsluga;

#define NAZWA_DL "ODZNAKA" 					//Nazwa uslugi wyswietlana w menu
#define NAZWA_KR "odznaka" 					//nazwa uslugi uzywana w logach, jako okienko MOTD itd.


new const g_szJednostkaIlosci[][] = {
	"Podstawowy",
	"Weteran" ,
	"Ekspert" 
	//Wypisz tutaj liste odznak
	//Pamietaj ze ostatnia usluga nie ma na koncu przecinka!
}

new const g_szCena[][][] = {
	{ "4,92", "4,92 zl SMS" },
	{ "6,15", "6,15 zl SMS" },
	{ "8,61", "8,61 zl SMS" }
	//Wypisz tutaj w takiej samej kolejnosci jak dlugosci uslug ich ceny
	//Format: "kr. cena", "dl. cena"
	//krotka cena - cena SMSa uslugi - zlotowki i grosze oddzielone przecinkiem
	//dluga cena - cena wyswietlana w menu
	//Pamietaj ze ostatnia linijka nie ma na koncu po klamrze przecinka!
}

native bf2_get_maxbadges();
native bf2_get_badge_name(badge_id, badge_level, Return[], len);
native bf2_get_user_badge(index, badge_id);
native bf2_set_user_badge(index, badge_id, level);

new g_iOstatnioWybranaOdznaka[33];

public plugin_init() 
{
	new szNazwa[64]; formatex(szNazwa, 63, "Sklep SMS: Usluga %s", NAZWA_DL);
	register_plugin(szNazwa, "1.0", "d0naciak");
	
	g_iUsluga = ss_register_service(NAZWA_DL, NAZWA_KR);


	for(new i = 0; i < sizeof g_szJednostkaIlosci; i++)
		ss_add_service_qu(g_iUsluga, g_szJednostkaIlosci[i], "-", g_szCena[i][1], g_szCena[i][0]);
	
}

public ss_buy_service_pre(id, iUsluga, iJednostkaIlosci)
{
	if(iUsluga != g_iUsluga)
		return SS_CONTINUE;

	new szNazwa[64], szId[4], iIloscOdznak = bf2_get_maxbadges();
	new iMenu = menu_create("Jaka odznake chcesz kupic?", "DlaCzegoOdznaka_Handler");
	
	for(new i = 0; i < iIloscOdznak; i++)
	{
		if(bf2_get_user_badge(id, i) >= (iJednostkaIlosci+1))
			continue;
		
		bf2_get_badge_name(i, iJednostkaIlosci+1, szNazwa, 63);
		num_to_str(i, szId, 3);
		
		menu_additem(iMenu, szNazwa, szId);
	}
	
	if(szId[0]) {
		menu_display(id, iMenu);
	} else {
		menu_destroy(iMenu);
		ColorChat(id, GREEN, "[SKLEP]^x01 Wszystkie odznaki na tym poziomie zostaly zdobyte!");
	}

	return SS_STOP;
}

public DlaCzegoOdznaka_Handler(id, iMenu, iItem)
{
	if(iItem == MENU_EXIT)
	{
		menu_destroy(iMenu);
		return PLUGIN_CONTINUE;
	}
	
	if(iItem < 0)
		return PLUGIN_CONTINUE;
	
	new iAccess, iCb, szId[4], iIdOdznaki, szNazwa[64];
	
	menu_item_getinfo(iMenu, iItem, iAccess, szId, 3, _, _, iCb);
	g_iOstatnioWybranaOdznaka[id] = iIdOdznaki = str_to_num(szId);
	
	bf2_get_badge_name(iIdOdznaki, ss_get_last_user_qu(id)+1, szNazwa, 63);
	ColorChat(id, GREEN, "[SKLEP]^x01 Wybrano odznake^x03 %s", szNazwa);

	ss_go_to_choosing_pay_method(id);
	menu_destroy(iMenu);

	return PLUGIN_CONTINUE;
}

public ss_buy_service_post(id, iUsluga, iJednostkaIlosci)
{
	if(iUsluga != g_iUsluga)
		return SS_CONTINUE;
	
	bf2_set_user_badge(id, g_iOstatnioWybranaOdznaka[id], iJednostkaIlosci+1);
	ss_finalize_user_service(id);
	
	return SS_CONTINUE;
}
