/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <colorchat>
#include <codmod>
#include <smsshop>

new g_iUsluga;

#define NAZWA_DL "Odznaka I stopnia" 				//Nazwa uslugi wyswietlana w menu
#define NAZWA_KR "odznaka1" 						//nazwa uslugi uzywana w logach, jako okienko MOTD itd.
#define CENA_KR "7,38"						//krotka cena SMS (wstawiaj przecinek, nie kropke!)
#define CENA_DL "7,38 zl SMS / 5 zl przelew / 5 zl PSC"		//dluga cena ktora wyswietlana bedzie w menu
#define JAKI_LEVEL 1						//na jaki poziom ma dawac odznake

native bf4_get_maxbadges();
native bf4_get_badge_name(badge_id, badge_level, Return[], len);
native bf4_get_user_badge(index, badge_id);
native bf4_set_user_badge(index, badge_id, level);

new g_iOstatnioWybranaOdznaka[33];

public plugin_init() 
{
	new szNazwa[64]; formatex(szNazwa, 63, "Sklep SMS: Usluga %s", NAZWA_DL);
	register_plugin(szNazwa, "1.0", "d0naciak");
	
	g_iUsluga = ss_register_service(NAZWA_DL, NAZWA_KR);
	ss_add_service_qu(g_iUsluga, "-", "0", CENA_DL, CENA_KR);
}

public ss_buy_service_pre(id, iUsluga, iJednostkaIlosci)
{
	if(iUsluga != g_iUsluga)
		return SS_CONTINUE;

	new szNazwa[64], szId[4], iIloscOdznak = bf4_get_maxbadges();
	new iMenu = menu_create("Jaka odznake chcesz kupic?", "DlaCzegoOdznaka_Handler");
	
	for(new i = 0; i < iIloscOdznak; i++)
	{
		if(bf4_get_user_badge(id, i) >= JAKI_LEVEL)
			continue;
		
		bf4_get_badge_name(i, JAKI_LEVEL, szNazwa, 63);
		num_to_str(i, szId, 3);
		
		menu_additem(iMenu, szNazwa, szId);
	}
	
	if(szId[0]) {
		menu_display(id, iMenu);
	} else {
		menu_destroy(iMenu);
		ColorChat(id, GREEN, "[SKLEP]^x01 Wszystkie odznaki na tym poziomie zostaly zdobyte!");
	}

	return SS_STOP;
}

public DlaCzegoOdznaka_Handler(id, iMenu, iItem)
{
	if(iItem == MENU_EXIT)
	{
		menu_destroy(iMenu);
		return PLUGIN_CONTINUE;
	}
	
	if(iItem < 0)
		return PLUGIN_CONTINUE;
	
	new iAccess, iCb, szId[4], iIdOdznaki, szNazwa[64];
	
	menu_item_getinfo(iMenu, iItem, iAccess, szId, 3, _, _, iCb);
	g_iOstatnioWybranaOdznaka[id] = iIdOdznaki = str_to_num(szId);
	
	bf4_get_badge_name(iIdOdznaki, JAKI_LEVEL, szNazwa, 63);
	ColorChat(id, GREEN, "[SKLEP]^x01 Wybrano odznake^x03 %s", szNazwa);

	ss_go_to_choosing_pay_method(id);
	menu_destroy(iMenu);

	return PLUGIN_CONTINUE;
}

public ss_buy_service_post(id, iUsluga, iJednostkaIlosci)
{
	if(iUsluga != g_iUsluga)
		return SS_CONTINUE;
	
	bf4_set_user_badge(id, g_iOstatnioWybranaOdznaka[id], JAKI_LEVEL);
	ss_finalize_user_service(id);
	
	return SS_CONTINUE;
}
