/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <colorchat>
#include <csgo>
#include <smsshop>

new g_iUsluga;

#define NAZWA_DL "Skrzynka" 				//Nazwa uslugi wyswietlana w menu
#define NAZWA_KR "skrzynki" 						//nazwa uslugi uzywana w logach, jako okienko MOTD itd.

new const g_szJednostkaIlosci[][][] = {
	{ "1", "1 skrzynka" },
	{ "4", "3 skrzynki" },
	{ "8", "8 skrzynek" }
	//Wypisz tutaj po kolei jednostke ilosci uslug
	//Format: "kr. jednostka", "dl. jednostka"
	//dluga jednostka - wyswietlana jest w np. menu
	//krotka jednostka - wykorzystywana w skryptach
	//Pamietaj ze ostatnia linijka nie ma na koncu po klamrze przecinka!
}

new const g_szCena[][][] = {
	{ "3,69", "3,69 zl SMS / 3 zl przelew / 3 zl PSC" },
	{ "11,07", "11,07 zl SMS / 8 zl przelew / 9 zl PSC" },
	{ "17,22", "17,22 zl SMS / 14 zl przelew / 16 zl PSC" }
	//Wypisz tutaj w takiej samej kolejnosci jak dlugosci uslug ich ceny
	//Format: "kr. cena", "dl. cena"
	//krotka cena - cena SMSa uslugi - zlotowki i grosze oddzielone przecinkiem
	//dluga cena - cena wyswietlana w menu
	//Pamietaj ze ostatnia linijka nie ma na koncu po klamrze przecinka!
}

new g_iOstatnioWybranaSkrzynka[33];

public plugin_init() 
{
	new szNazwa[64]; formatex(szNazwa, 63, "Sklep SMS: Usluga %s", NAZWA_DL);
	register_plugin(szNazwa, "1.0", "d0naciak");
	
	g_iUsluga = ss_register_service(NAZWA_DL, NAZWA_KR);
	
	for(new i = 0; i < sizeof g_szJednostkaIlosci; i++)
		ss_add_service_qu(g_iUsluga, g_szJednostkaIlosci[i][1], g_szJednostkaIlosci[i][0], g_szCena[i][1], g_szCena[i][0]);
}

public ss_buy_service_pre(id, iUsluga, iJednostkaIlosci)
{
	if(iUsluga != g_iUsluga)
		return SS_CONTINUE;
	
	WybierzSkrzynke(id);
	return SS_STOP;
}


public WybierzSkrzynke(id) {
	new iMenu = menu_create("Wybierz skrzynke:", "WybierzSkrzynke_Handler"),
	szNazwa[32];
	
	for(new i = 1; i <= csgo_get_cratesnum(); i++)
	{
		csgo_get_crate_name(i, szNazwa, 31);
		
		menu_additem(iMenu, szNazwa);
	}
	
	menu_display(id, iMenu);
}

public WybierzSkrzynke_Handler(id, iMenu, iItem) {
	if(iItem < 0) {
		if(iItem == MENU_EXIT) {
			menu_destroy(iMenu);
		}
		
		return PLUGIN_CONTINUE;
	}
	
	new szNazwa[64];
	
	g_iOstatnioWybranaSkrzynka[id] = iItem + 1;
	
	csgo_get_crate_name(iItem+1, szNazwa, 63);
	ColorChat(id, NORMAL, "Wybrales^x03 %s", szNazwa);
	
	ss_go_to_choosing_pay_method(id);
	
	menu_destroy(iMenu);
	return PLUGIN_CONTINUE;
}

public ss_buy_service_post(id, iUsluga, iJednostkaIlosci)
{
	if(iUsluga != g_iUsluga)
		return SS_CONTINUE;
	
	new iSkrzynka = g_iOstatnioWybranaSkrzynka[id];
	csgo_set_user_crates(id, iSkrzynka, csgo_get_user_crates(id, iSkrzynka) + str_to_num(g_szJednostkaIlosci[iJednostkaIlosci][0]));
	
	ss_finalize_user_service(id);
	
	return SS_CONTINUE;
}


