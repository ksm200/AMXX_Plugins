/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <codmod>
#include <colorchat>
#include <smsshop>


//UWAGA! UWAGA! UWAGA! UWAGA! UWAGA! UWAGA! UWAGA! UWAGA! 
//Usluga ta nie dziala, jezeli korzystasz z awansow klas

new g_iUsluga;

new g_iPrzeniesZKlasyGracza[33];
new g_iPrzeniesDoKlasyGracza[33];

#define NAZWA_DL "Przenies doswiadczenie" 			//Nazwa uslugi wyswietlana w menu
#define NAZWA_KR "przeniesexp" 					//nazwa uslugi uzywana w logach, jako okienko MOTD itd.
#define CENA_KR "7,38"						//krotka cena SMS (wstawiaj przecinek, nie kropke!)
#define CENA_DL "7,38 zl SMS / 5 zl przelew / 5 zl PSC"		//dluga cena ktora wyswietlana bedzie w menu

public plugin_init() 
{
	new szNazwa[64]; formatex(szNazwa, 63, "Sklep SMS: Usluga %s", NAZWA_DL);
	register_plugin(szNazwa, "1.0", "d0naciak");
	
	g_iUsluga = ss_register_service(NAZWA_DL, NAZWA_KR);
	ss_add_service_qu(g_iUsluga, "-", "0", CENA_DL, CENA_KR);
}

public ss_buy_service_pre(id, iUsluga, iJednostkaIlosci)
{
	if(iUsluga != g_iUsluga)
		return SS_CONTINUE;
	
	new iMenu = menu_create("Wybierz klase z ktorej chcesz przeniesc doswiadczenie", "PrzeniesXPZKlasy_Handler"), iIlosc = cod_get_classes_num();
	new szNazwaKlasy[32];

	for(new i = 1; i <= iIlosc; i++)
	{
		cod_get_class_name(i, szNazwaKlasy, 31);
		menu_additem(iMenu, szNazwaKlasy);
	}
	
	menu_display(id, iMenu);
	
	return SS_STOP;
}

public PrzeniesXPZKlasy_Handler(id, iMenu, iItem)
{
	if(iItem < 0)
	{
		if(iItem == MENU_EXIT)
			menu_destroy(iMenu);
			
		return PLUGIN_CONTINUE;
	}
	
	new iKlasa = g_iPrzeniesZKlasyGracza[id] = iItem + 1, szNazwaKlasy[32];
	
	cod_get_class_name(iKlasa, szNazwaKlasy, 31);

	ColorChat(id, GREEN, "[SKLEPSMS]^x01 Wybrales klase^x03 %s", szNazwaKlasy);

	new iMenu2 = menu_create("Wybierz klase na ktora chcesz przeniesc doswiadczenie", "PrzeniesXPDoKlasy_Handler"), iIlosc = cod_get_classes_num();
			
	for(new i = 1; i <= iIlosc; i++)
	{
		cod_get_class_name(i, szNazwaKlasy, 31);
		menu_additem(iMenu2, szNazwaKlasy);
	}
			
	menu_display(id, iMenu2);
	
	return PLUGIN_CONTINUE;
}

public PrzeniesXPDoKlasy_Handler(id, iMenu, iItem)
{
	if(iItem < 0)
	{
		if(iItem == MENU_EXIT)
			menu_destroy(iMenu);
			
		return PLUGIN_CONTINUE;
	}
	
	new iKlasa = g_iPrzeniesDoKlasyGracza[id] = iItem + 1, szNazwaKlasy[32];
	
	cod_get_class_name(iKlasa, szNazwaKlasy, 31);

	ColorChat(id, GREEN, "[SKLEPSMS]^x01 Wybrales klase^x03 %s", szNazwaKlasy);
	
	ss_go_to_choosing_pay_method(id);
	
	menu_destroy(iMenu);
	return PLUGIN_CONTINUE;
}

public ss_buy_service_post(id, iUsluga, iJednostkaIlosci)
{
	if(iUsluga != g_iUsluga)
		return SS_CONTINUE;
	
	new iKlasaOd = g_iPrzeniesZKlasyGracza[id], iKlasaDo = g_iPrzeniesDoKlasyGracza[id];

	cod_set_user_class(id, iKlasaOd, 1);
	new iExp = cod_get_user_xp(id);
	cod_set_user_xp(id, 0);
	cod_set_user_class(id, iKlasaDo, 1);
	cod_set_user_xp(id, cod_get_user_xp(id) + iExp);

	ss_finalize_user_service(id);
	
	return SS_CONTINUE;
}
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1045\\ f0\\ fs16 \n\\ par }
*/
